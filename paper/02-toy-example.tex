%!TeX root = demotion-paper.tex
\documentclass[demotion-paper.tex]{subfiles}
\begin{document}
\section{Toy Example: Demoting Type-level GCD}
\label{sec:gcd}
Let us begin with a simple example of type-level greatest common divisors (GCDs):

\begin{code}
import GHC.TypeLits

type family GCD n m where
  GCD 0 m = m
  GCD n 0 = n
  GCD n m = GCD (Mod m n) n
\end{code}

So far, so good.

\begin{repl}
>>> :kind! GCD 12 9
GCD 12 9 :: Nat
= 3
\end{repl}

Suppose we want to ``demote'' this definition of \hask{GCD} to expression-level using singletons, that is, we want to implement the following function \hask{sGCD}:

\begin{code}
sGCD :: SNat n -> SNat m -> SNat (GCD n m)
\end{code}

First, we need to test the equality of type-level naturals.
In the \texttt{base} package, there is a suitable type-classs for it:
\begin{code}
-- Defined in Data.Type.Equality in base
class TestEquality f where
  testEquality :: f a -> f b -> Maybe (a :~: b)

data (:~:) a b where Refl :: a :~: a
\end{code}
Assuming the \hask{TestEquality SNat} instance, one might first attempt to write it as follows:
\begin{code}
sGCD :: SNat n -> SNat m -> SNat (GCD n m)
sGCD sn sm =
  case ( testEquality sn (sNat @0), 
         testEquality sm (sNat @0)) of
    (Just Refl, _) -> sm
    (_, Just Refl) -> sn
    (Nothing, Nothing) -> sGCD (sMod sm sn) sn
\end{code}

The first two lines type-checks as expected, but the last case results in the following type error:

\begin{repl}
• Couldn't match type ‘GCD (Mod m n) n’
  with ‘GCD n m’
  Expected type: SNat (GCD n m)
    Actual type: SNat (GCD (Mod m n) n)
  NB: ‘GCD’ is a non-injective type family
  ...
\end{repl}

Why? The definition of \hask{sGCD} seems almost literally the same as type-level \hask{GCD}.
It first match \hask{n} against \hask{0}, then \hask{m} against \hask{0}, and finally fallbacks to \hask{GCD (Mod m n) n}.

Carefully analysing the first two cases, one can realise that there are additional type-level constraints introduced by \hask{Refl} GADT constructor:

\begin{code}
  Refl :: a ~ b => a :~: b
\end{code}

Thus, in the first two cases, the compiler can tell either \hask{n ~ 0} or \hask{m ~ 0}.
Since the \hask{GCD} is defined as a closed type family, the compiler can match clauses in a top-down manner and successfully apply either of the first two clauses of the definition of \hask{GCD}.
In other words, the constructor \hask{Refl} \emph{witnesses} the evaluation path of type-level function \hask{GCD} in the first two cases.

In the last case, however, no additional type-level constraint is available.
Despite humans can still think ``as all the \hask{Refl} clauses failed to match, hence the non-equal clause must apply here'', this intuition is not fully expressed in the type-level constraint!

So we have to give the compiler some \emph{witness} also in the last case.
What kind of witness is needed here?
Well, we need to teach the compiler which clause was actually used.
In this case, branching is caused by type-level equality: the evaluation path depends on whether wither \hask{n} or \hask{m} is \hask{0} or not.
First, let us make this intuition clear in the definition of \hask{GCD}:

\begin{code}
import Data.Type.Equality (type (==)) -- from base

type GCD n m = GCD_ (n == 0) (m == 0) n m
type family GCD_ nEq0 mEq0 n m :: Nat where
  GCD_ 'True  _      _ m = m   -- n ~ 0; return m
  GCD_ 'False 'True  n _ = n   -- m ~ 0; return n
  GCD_ 'False 'False n m = 
    GCD_ (Mod m n == 0) 'False (Mod m n) n
\end{code}

Here, we have two type-level functions: newly defined one, \hask{GCD_}, is the main loop implementing Euclidean algorithm, and
\hask{GCD} is redefined to call \hask{GCD_} with the needed information.
Now, \hask{GCD_} takes not only natural numbers but also a type-level \hask{Bool}ean-values \emph{witnessing} equality of \hask{n} and \hask{m} with \hask{0}.
From this, GHC can tell which clause is taken from the first two type-arguments.
As clauses in closed type families can be viewed as a mutually exclusive alternatives, this approach shares the spirit with the constructive interpretation of $\vee$.

Now that we can give the compiler witnesses as the first two type-arguments of \hask{GCD_}, we are set to implement \hask{sGCD}.
First, we need \emph{demoted} version of type-level \hask{(==)}.
The first attempt might go as follows:
\begin{code}
(%==) :: TestEquality f
      => f a -> f b -> SBool (a == b)
sa %== sb = case testEquality sa sb of
  Just Refl -> STrue
  Nothing -> SFalse
\end{code}
Unfortunately, this doesn't work as expected.
The first error on \hask{STrue} says:

\begin{repl}
• Could not deduce: (a == a) ~ 'True
  from the context: b ~ a
    bound by a pattern with constructor:
               Refl :: forall k (a :: k). a :~: a,
             in a case alternative
    at /.../GCD.hs:33:8-11
  Expected type: SBool (a == b)
    Actual type: SBool 'True
\end{repl}

This is due to the definition of type-level \hask{(==)} in GHC base library:

\begin{code}
type family a == b where
  f a == g b = (f == g) && (a == b)
  a   == a   = 'True
  _   == _   = 'False
\end{code}

The complication of the first clause is intensional.
As described in the documentation~\cite{GHC-Team:2021aa}, the intuition behind this is to let the compiler to infer, e.g.\ \hask{Just a == Just b} from \hask{a == b}.

This behaviour is desirable when one \emph{introduces} the constraints of forms \hask{(f a == g b) ~ 'True}.
But when one wants to \emph{eliminate} an evidence of \hask{(a == b) ~ 'True}, we cannot make use of the evidence given by \hask{Refl}.
This is, again, due to the lack of witness of the evaluation path: the compiler cannot determine which clause should be taken to compute \hask{a == b} if \hask{a} and \hask{b} are both opaque variable!

A solution here is just to define another type family, which requires only bare equality:
\begin{code}
type family a === b where
  a === a = 'True
  _ === _ = 'False
\end{code}

Although this equality cannot treat equalities between compound types inductively, it suffices for \hask{GCD} case.
We will revisit to a treatment of type-level equality in \Cref{sec:plugins}.
Demoted version of this now gets:

\begin{code}
(%===) :: TestEquality f
       => f a -> f b -> SBool (a === b)
sa %=== sb = case testEquality sa sb of
  Just Refl -> STrue
  Nothing -> SFalse  
\end{code}

Now, the type-error remains on the last clause: \hask{SFalse}.
This is, again, due to the lack of witness of being distinct.
But wait! We are just struggling to produce such a negative witness of non-equality, which in turn requires itself. A vicious cycle!
At this very point, there is no other way than resorting to the ancient cursed spell: \hask{unsafeCoerce}:
\begin{code}
import Unsafe.Coerce

sa %=== sb = case testEquality sa sb of
  Just Refl -> STrue
  Nothing -> unsafeCoerce SFalse  
\end{code}
This use of \hask{unsafeCoerce} is inherently inevitable.
Fortunately, provided that \hask{TestEquality} instance is implemented soundly, this use of \hask{unsafeCoerce} is not cursed: this is just postulating an axiom that is true but there is no way to tell it to the compiler safely.
If one wants to construct evidence of type-level (non-)equality solely from the expression, we must assume some axiom and introduce it by \hask{unsafeCoerce}.
This is how library builders usually do when they implement basic (expression-level) operators to manipulate type-level values.
Such ``trust me'' axioms can be found, for example, in \hask{TestEquality TypeRep} instance in \texttt{base}, and various \hask{SEq} instances in \texttt{singletons}~\cite{singletons} package\footnote{There is another way of introducing axioms: invoking type-checker plugins.}.
Anyway, we are finally at the point of implementing working \hask{sGCD}, replacing every occurrence of \hask{(==)} with our custom \hask{(===)}:

\begin{code}
type GCD n m = GCD_ (n === 0) (m === 0) n m

type family GCD_ nEq0 mEq0 n m :: Nat where
  GCD_ 'True  _      _ m = m
  GCD_ 'False 'True  n _ = n
  GCD_ 'False 'False n m = 
    GCD_ (Mod m n === 0) 'False (Mod m n) n

sGCD :: SNat n -> SNat m -> SNat (GCD n m)
sGCD sn sm =
  case (sn %=== sZero, sm %=== sZero) of
    (STrue, _) -> sm
    (SFalse, STrue) -> sn
    (SFalse, SFalse) -> sGCD (sMod sm sn) sn
\end{code}

Finally, the compiler gets happy with all the definitions!
We can confirm that the above \hask{sGCD} works just as expected:
\begin{repl}
>>> sGCD (sNat @12) (sNat @30)
6
\end{repl}

\subsection{Summary}
When writing a closed type-family involving pattern-matchings, it is useful to introduce arguments witnessing evaluation paths explicitly, interpreting clauses in a closed family as mutually exclusive alternatives.
It makes it easy to write its demoted singletonised functions.
As there are several variants of type-level equality; in this case it is convenient to use the type-level Boolean equality which takes only reflexivity into account.
\end{document}
