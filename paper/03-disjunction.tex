%!TeX root = demotion-paper.tex
\documentclass[demotion-paper.tex]{subfiles}
\begin{document}
\section{Disjunctive Constraints}\label{sec:disj}
It is sporadically complained that type-classes in Haskell lack \emph{disjunction}, or \emph{logical-or}.
As for general type-classes, excluding disjunction from the language is a rational design decision for several reasons:
\begin{enumerate}
  \item Haskell adopts the open-world hypothesis for type-classes: users can add new instances, so the result of the instance resolution can differ in context.
  \label{item:openness}
  \item The semantics is not clear when multiple disjunctive clauses can be satisfied simultaneously.
  \label{item:arb-choice}
\end{enumerate}
In some cases, however, the above obstacles can be ignored, and in most cases, unrestricted disjunctions are unnecessary.
For example, when one wants to switch implementations of instances based on particular shapes of a constructor, it is customary to use \hask{{-# OVERLAPPABLE #-}} or \hask{{-# INCOHERENT #-}} pragmas.
In some cases, this makes instance resolution unpredictable and incoherent, as indicated by the name, and sometimes doesn't work well with advanced type hackery.

We propose another way to emulate disjunctive constraints, applying the constructive point of view.
First, we recall the ``meaning'' of disjunction $\varphi \vee \psi$ in BHK interpretation:
\begin{gather*}
      A: \varphi \vee \psi \iff A = \braket{i, B},
    \\
    \text{where }
    i = 0 \text{ and } B: \varphi, \text{ or }
    i = 1 \text{ and } B: \psi.
\end{gather*}
That is, a ``witness'' of $\varphi \vee \psi$ is a union of those of $\varphi$ and $\psi$.

It suggests that if constraints of interest can be recovered from \emph{witnesses}, we can take their disjunction by choosing one of such witnesses.
If those witnesses can be computed statically and deterministically, the obstacle \Cref{item:openness} is not a problem.
Further, if users can explicitly \emph{manipulate} such witnesses concretely, we can control the selection strategy of disjunctive clauses, enabling us to resolve \Cref{item:arb-choice} manually.

\subsection{Example: Field Accessors of Union Types}
OK, let us see the example.
Here, we consider the following \hask{HasFactor}-class:

\begin{code}
class HasFactor a b where
  getFactor :: b -> a
\end{code}

The intended semantics of \hask{HasFactor a b} is that ``a type \hask{b} has at least one field of type \hask{a}'', and \hask{getFactor} is a corresponding field accessor.
In short, the problem we want to tackle in this section is as follows:
\begin{quote}
  \emph{How can we give a} \hask{HasFactor a}\emph{-instance for union types}?
\end{quote}

\subsubsection{Extensible Records}
To make the situation simpler, let us introduce another player into the scene: the type \hask{Record h ks} of \emph{extensible records}.
That is, the type \hask{Record h ks} is isomorphic to a record with field labels \hask{ks :: [key]}, where each label \hask{k :: key} in \hask{ks} is associated with a value of type \hask{h k}.
Here, \hask{h :: key -> Type} gives an actual container type for each label; for example, \hask{Record Maybe '[Int, String, Bool]} represents the type of record with fields of types \hask{Maybe Int}, \hask{Maybe String} and \hask{Maybe Bool}.
In what follows, we implement \hask{Record} as a heterogeneous linked-list\footnotemark:
\footnotetext{%
In practice, it is much more convenient to allow specifying labels and corresponding field type independently, i.e.\ parametrise \hask{Record} over \hask{h :: key -> val -> Type}.
And for efficiency, the underlying representation of an extensible record should be an array or vector with $O(1)$ random access.
Furthermore, it is desirable to use type-level ordered maps instead of lists to tune-up type-checking speed and prevent duplicated labels statically.
Several efficient implementations can be found on Hackage~\cite{Kinoshita:2020aa,Sterling:2020aa,Thiemann:2020aa}, and we are also planning to publish the in-house package (still in progress, though).}
\begin{code}
data Record h keys where
  EmptyRecord :: Record h '[]
  (:<) :: h k -> Record h ks -> Record h (k ': ks)
\end{code}
We also need a field accessor to retrieve information from a record:
\begin{code}
data Index k ks where
  Here  :: Index k (k ': ks)
  There :: Index k ks -> Index k (k' ': ks)

-- ...Obvious singletons and Known instances...

walkIndex :: Index k ks -> Record h ks -> h k
walkIndex Here (v :< _) = v
walkIndex (There trail) (_ :< rest) = 
  walkIndex trail rest  
\end{code}
A type \hask{Index k ks} is a \emph{witness} of the membership of a label \hask{k} in a type-level list \hask{ks}.
The function \hask{walkIndex} walks an extensible record along a given \hask{Index k ks} and retrieves a value of \hask{h k}\footnote{The same remark on efficiency as above also applies here: it is much more practical to use an index number represented as a newtyped \hask{Int} if one needs $O(1)$ random access on fields.
In such a case, one has to use \hask{unsafeCoerce} carefully to convince the compiler.}.

We can also compute \hask{Index} at type-level in an obvious way, where \haskinline{(<$>)} is a type-level analogue of \hask{(<$>)}:
\begin{code}
type family FindIndex k ks where
  FindIndex _ '[] = 'Nothing
  FindIndex k (k ': ks) = 'Just 'Here
  FindIndex k (_ ': ks) =
    'There <$> FindIndex k ks  

type FindIndex' k ks = 
  FromJust ('Text "not found") (FindIndex k ks)
\end{code}
\hask{FindIndex'} is a variant of \hask{FindIndex}, which returns a raw membership if present, and throws a type-error otherwise.

Now that we can compute the indices of labels statically, one can implement a variant of \hask{walkIndex} with the index statically inferred:
\begin{code}
type Member k ks = Known (FindIndex' k ks)
getRecField :: forall k ks h.
  Member k ks => Record h ks -> h k
getRecField = walkIndex $ 
  demote $ sing @(FindIndex' k ks)
\end{code}
The idea is that if the concrete value of \hask{FindIndex' k ks} is \hask{Known} at the compile-time, we can use it to retrieve a field in a record.
Note that if the value of \hask{FindIndex k ks} was \hask{'Nothing}, \hask{FindIndex' k ks} reduces to a type-error.
In such a case, since the type-level language of Haskell is strict, the entire constraint \hask{Known (FindIndex' k ks)} throws a type-error during instance resolution:
\begin{repl}
>>> getRecField @Bool (EmptyRecord @Maybe)
Key `Bool' is absent in the list: '[]

>>> getRecField @Bool (['a'] :< [True, False]
                :< ([] :: [()]) :< EmptyRecord)
[True, False]
\end{repl}

Now, we can give an implementation of \hask{HasFactor} for records:
\begin{code}
instance (Member a keys, h ~ h')
      => HasFactor (h' a) (Record h keys) where
  getFactor = getRecField
\end{code}

\subsubsection{Factor of a Union}
Now we enter the situation where we want disjunctive constraints.
Consider the following type paring two records together:
\begin{code}
data RecUnion h ls rs = 
  UnionRec { recL :: Record h ls
           , recR :: Record h rs }
\end{code}
Now, how can we implement instances of \hask{HasFactor} for \hask{RecUnion}s?
Na\"{i}vely, one might at first want to write an instance like:
\begin{code}
instance (HasFactor (h a) (Record h ls)
          `Or` HasFactor (h a) (Record h rs))
  => HasFactor (h a) (RecUnion h ls rs) where ...
\end{code}
But, as noted before, there is no such thing in Haskell as restricted \hask{Or}-constraint.

Fortunately, our \hask{HasFactor (h a) (Record h ls)} instance is \emph{witnessed} by a concrete type: \hask{Index a ls}!
Since its concrete value can be computed statically by \hask{FindIndex} type-family, why not combine the results of them\footnote{Here, \hask{(<|>)} is a type-level left-biased choice operator on \hask{Maybe}s.}?
\begin{code}
type UnionedIdnex k ls rs = 
  'Left <$> FindIndex k ls
  <|> 'Right <$> FindIndex k rs
type UnionedIndex' k ls rs = FromJust 
  ('Text "No label found") (UnionedIndex k ls rs)

instance Known (UnionedIndex' a ls rs)
  => HasFactor (h a) (RecUnion h ls rs) where
  getFactor (UnionRec l r) =
    case sing @(UnionedIndex' a ls rs) of
      SLeft pth -> withKnown pth $ getFactor l
      SRight pth -> withKnown pth $ getFactor r
\end{code}
Everything seems fine, but then GHC complains on call-sites of \hask{getFactor}:
\begin{repl}
â€¢ Could not deduce 
  (Known (FromJust (...) (FindIndex a ls)))...
\end{repl}
The error seems weird at first glance: we are just giving the \hask{Known} dictionary with \hask{withKnown pth}, where \hask{pth} is of type either \hask{Index a ls} or \hask{Index a rs}. Why?

The root cause of this error is that the type-checker doesn't know the following facts:
\begin{enumerate}
  \item \hask{FromJust ('Text ...)} commutes with \haskinline{(<$>)}, and
  \item If \hask{FromJust x m} reduces, then \hask{m ~ 'Just (FromJust z m)} for any \hask{z}.
\end{enumerate}
Although these two facts seem rather obvious, it needs some non-trivial axioms to infer.
Hence, if we want to convince the compiler without modifying the instance definitions, we have to augment the compiler with type-checker plugins~\cite{GHC-Team:2020aa}.
Although writing type-checker plugins is a fun, but it is not so easy to implement it \emph{correctly}. Is there any other way to avoid this obstacle?

At this point, we must notice one fact: we can still use \emph{any} value of type \hask{Index k ks} to retrieve a value of type \hask{h k} from \hask{Record h ks}.
Indeed, as \hask{ks} can have duplicated elements, there can be more than one distinct \hask{Index k ks} at the same time, e.g.\ for \hask{Index 3 '[3 ,5, 4, 3]}.
\hask{FindIndex} was just a canonical way of computing the left-most such index, if present.
To summarise, requiring \hask{Known (LookupIndex' k ks)} in \hask{getRecField} and the \hask{HasFactor}-instance for extensible records was just too much.
What we really need is a constraint demanding ``there is at least one value of type \hask{Index k ks} given'', embodied by the following class and helper functions:
\begin{code}
class Given a where
  given :: a

give :: a -> (Given a => r) -> r
\end{code}
These are excerpted from widely-used \texttt{reflection} package~\cite{Kmett:2020aa} which implements Implicit Configuration~\cite{Kiselyov:2004aa}.

With this, we can rewrite \hask{getRecField} and the corresponding \hask{HasFactor}-instance in more robust way:
\begin{code}
type Member k ks = Given (Index k ks)

getRecField
  :: Member key keys => Record h keys -> h key
getRecField = walkIndex given

-- | Serves as a default instance for @Member@.
-- Can be safely overridden by 'give' operator.
instance Known (FindIndex' k ks)
      => Given (Index k ks) where
  given = demote $ sing @(FindIndex' k ks)

instance (Given (Index a keys), h ~ h')
      => HasFactor (h' a) (Record h keys) where
  getFactor = getRecField
\end{code}

With this, we can now successfully implement \hask{HasFactor}-instance for \hask{RecUnion} as follows:

\begin{code}
-- To avoid orphan @Given@ instance
newtype IndexUnion k ls rs = WrapIdxUnion
  {unUnionIdx :: Either (Index k ls) (Index k rs)}

instance Known (UnionedIndex' k ls rs)
  => Given (IndexUnion k ls rs) where
  given = WrapIdxUnion $ demote $ 
    sing @(UnionedIndex' k ls rs)

instance Given (IndexUnion k ls rs)
      => HasFactor (h k) (RecUnion h ls rs) where
  getFactor (UnionRec l r) =
    case unUnionIdx
      $ given @(IndexUnion k ls rs) of
      Left pth -> give pth $ getFactor l
      Right pth -> give pth $ getFactor r
\end{code}

In above two implementations, \hask{Given} instances serve as a ``default instances'' to calculate witnesses.
As already shown in the implementation of \hask{getFactor} for \hask{UnionRec}, it can be overridden by \hask{give} operator without resulting in type errors complaining about overlapping instances or ``Could not deduce (Known ...)''.
This flexibility is the reason we chose to use \hask{Given} class instead of the \texttt{ImplicitParams} GHC extension providing similar functionality of dynamic scoping:  the instance shadowing in \texttt{ImplicitParams} can have unpredictable behaviour.
Such ``defaulting'' cannot be done for \hask{Known}, because type family applications such as \hask{FindIndex k ks} cannot appear at RHS of instance declarations.

Let us check it works as expected:

\begin{repl}
>>> theRec = Const "Hehe" :< Const "Foo"
      :< EmptyRecord
      :: Record (Const String) '[5,42]
>>> anotherRec = Const "Phew" :< Const "Wow"
      :< EmptyRecord
      :: Record (Const String) '[94, 5]
>>> unioned = UnionRec theRec anotherRec
>>> getFactor @(Const String 42) unioned
Const "Foo"

>>> getFactor @(Const String 94) unioned
Const "Phew"

>>> getFactor @(Const String 5) unioned
Const "Hehe"

-- Beware of reordering:
>>> getFactor @(Const String 5) 
      (UnionRec anotherRec theRec)
Const "Wow"

>>> getFactor @(Const String 999) unioned
A field of type `999' not found in either:
   Left: '[5, 42]
  Right: '[94, 5]
\end{repl}

\subsection{Summary}
We saw that we could emulate a disjunction of type constraints if the constraints in question have \emph{witnessing} type, and there is a canonical way of statically computing such witnesses at type-level.
In the above example, \hask{Member k ks} and \hask{HasFactor (h a) (RecUnion h ls rs)} are such constraints, witnessed by \hask{Index k ks} and \hask{IndexUnion k ls rs}, and such witnesses can be computed by \hask{FindIndex'} and \hask{UnionedIndex'}, respectively.
The \hask{Given} class allows us to express the existence of witnesses with a precise and robust handling of constraint resolutions.
Especially, this makes it easy to give ``fallback'' witness when canonical witness constructor can fail.
Although we treated only extensible records here, we can apply the same technique to more general settings;
we refer curious readers to \texttt{Data.Type.Path} module %
\ifbool{maingo}{%
  in the accompanying repository~\cite{demotion-repo}%
}{in the accompanying supplementary material}.
\end{document}
